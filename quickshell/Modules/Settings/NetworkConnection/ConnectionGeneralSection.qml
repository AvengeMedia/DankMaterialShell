import QtQuick
import Quickshell
import qs.Common
import qs.Modals.FileBrowser
import qs.Services
import qs.Modules.Settings.Widgets
import qs.DCommon.Widgets
import "../../../Common/ConnectionEditor.js" as ConnectionEditor

SettingsCard {
    id: root

    required property var editor
    readonly property bool valid: name !== ""

    readonly property int rev: editor.revision
    readonly property string name: {
        rev;
        return String(editor.value("connection", "id", "")).trim();
    }
    readonly property bool editable: !editor.readOnly
    property var zones: []

    readonly property string connType: {
        rev;
        return String(editor.value("connection", "type", ""));
    }
    readonly property string serviceType: {
        rev;
        return String(editor.value("vpn", "service-type", ""));
    }
    readonly property string uuid: {
        rev;
        return String(editor.profile?.uuid ?? editor.value("connection", "uuid", ""));
    }
    readonly property bool vpnTypes: NetworkService.vpnEditorSupported
    readonly property var typeOptions: {
        const out = [
            {
                "value": "802-3-ethernet",
                "label": I18n.tr("Ethernet")
            },
            {
                "value": "802-11-wireless",
                "label": I18n.tr("Wi-Fi")
            }
        ];
        if (vpnTypes) {
            out.push({
                "value": "wireguard",
                "label": "WireGuard"
            });
            for (const p of VPNService.plugins ?? []) {
                out.push({
                    "value": "vpn:" + p.serviceType,
                    "label": VPNService.getPluginName(p.serviceType)
                });
            }
        }
        if (NetworkService.nmParitySupported) {
            out.push({
                "value": "vlan",
                "label": "VLAN"
            }, {
                "value": "bond",
                "label": I18n.tr("Bond", "network connection type: bonded interfaces")
            }, {
                "value": "bridge",
                "label": I18n.tr("Bridge", "network connection type: network bridge")
            }, {
                "value": "pppoe",
                "label": "DSL"
            }, {
                "value": "gsm",
                "label": I18n.tr("Cellular")
            });
        }
        if (ConnectionEditor.groupOf(SettingsUiState.newConnectionType) === "vpn")
            return out.filter(t => ConnectionEditor.groupOf(t.value) === "vpn");
        return out;
    }
    readonly property string typeValue: connType === "vpn" ? "vpn:" + serviceType : connType
    readonly property var meteredLabels: [I18n.tr("Auto"), I18n.tr("Yes"), I18n.tr("No")]
    readonly property var vpnProfiles: {
        rev;
        return (editor.profiles ?? []).filter(p => ConnectionEditor.groupOf(p.type) === "vpn");
    }

    // Export writes the saved profile, so the name comes from it rather than the draft.
    function exportFileName() {
        const saved = editor.original?.connection ?? {};
        const id = String(saved.id ?? "");
        if (connType === "wireguard")
            return String(saved["interface-name"] || id) + ".conf";
        return id + (serviceType.endsWith(".openvpn") ? ".ovpn" : ".conf");
    }

    function openExport() {
        exportBrowser.active = true;
        if (exportBrowser.item)
            exportBrowser.item.open();
    }

    // 3 and 4 (guess yes/no) display as Auto
    function meteredIndex() {
        const m = Number(editor.value("connection", "metered", 0));
        return m === 1 ? 1 : (m === 2 ? 2 : 0);
    }

    function storedZone() {
        return String(editor.value("connection", "zone", ""));
    }

    function zoneOptions() {
        const stored = storedZone();
        const opts = [I18n.tr("Default")].concat(zones);
        if (stored !== "" && zones.indexOf(stored) < 0)
            opts.push(stored);
        return opts;
    }

    function storedVpn() {
        const list = editor.value("connection", "secondaries", []);
        return list && list.length > 0 ? String(list[0]) : "";
    }

    // Index 0 is None; repeated names get a " (n)" suffix so each label maps to one profile.
    function vpnOptions() {
        const seen = {};
        return [I18n.tr("None")].concat(vpnProfiles.map(p => {
            const n = seen[p.id] = (seen[p.id] || 0) + 1;
            return n > 1 ? p.id + " (" + n + ")" : p.id;
        }));
    }

    function vpnLabel() {
        const uuid = storedVpn();
        if (uuid === "")
            return I18n.tr("None");
        const i = vpnProfiles.findIndex(x => x.uuid === uuid);
        return i >= 0 ? vpnOptions()[i + 1] : uuid;
    }

    title: I18n.tr("General")

    headerActions: Row {
        spacing: Theme.spacingXS

        DActionButton {
            visible: !root.editor.isNew && (root.connType === "wireguard" || root.connType === "vpn")
            iconName: "file_save"
            iconSize: Theme.iconSizeSmall
            tooltipText: I18n.tr("Export", "save a connection profile to a file")
            onClicked: root.openExport()
        }

        DActionButton {
            visible: !root.editor.isNew && NetworkService.nmConnectionEditorAvailable
            iconName: "open_in_new"
            iconSize: Theme.iconSizeSmall
            tooltipText: I18n.tr("Open in %1", "%1 is an application name").arg("nm-connection-editor")
            onClicked: NetworkService.openInNmConnectionEditor(root.uuid)
        }
    }

    LazyLoader {
        id: exportBrowser
        active: false

        FileBrowserModal {
            browserTitle: I18n.tr("Export", "save a connection profile to a file")
            bucket: "vpn"
            saveMode: true
            defaultFileName: root.exportFileName()
            onAccepted: paths => {
                NetworkService.exportConnection(root.uuid, paths[0], r => {
                    if (r?.error)
                        ToastService.showError(I18n.tr("Failed to export"), r.error);
                    else
                        ToastService.showInfo(I18n.tr("Saved"));
                });
            }
        }
    }

    Component.onCompleted: {
        NetworkService.probeNmConnectionEditor();
        if (vpnTypes && (VPNService.plugins ?? []).length === 0)
            VPNService.fetchPlugins();
        NetworkService.listFirewallZones(response => {
            const list = response?.result;
            if (Array.isArray(list))
                root.zones = list;
        });
    }

    SettingsDropdownRow {
        visible: root.editor.isNew
        enabled: root.editable
        text: I18n.tr("Type")
        options: root.typeOptions.map(t => t.label)
        currentValue: root.typeOptions.find(t => t.value === root.typeValue)?.label ?? ""
        onValueChanged: value => {
            const t = root.typeOptions.find(x => x.label === value);
            if (t && t.value !== root.typeValue)
                root.editor.resetNew(t.value);
        }
    }

    SettingsTextFieldRow {
        enabled: root.editable
        text: I18n.tr("Name")
        value: {
            root.rev;
            return String(root.editor.value("connection", "id", ""));
        }
        isError: root.name === ""
        onValueEdited: value => root.editor.setValue("connection", "id", value)
    }

    SettingsToggleRow {
        enabled: root.editable
        text: I18n.tr("Autoconnect")
        checked: {
            root.rev;
            return root.editor.value("connection", "autoconnect", true) !== false;
        }
        onToggled: checked => root.editor.setValue("connection", "autoconnect", checked)
    }

    SettingsTextFieldRow {
        enabled: root.editable
        text: I18n.tr("Priority")
        value: {
            root.rev;
            return String(root.editor.value("connection", "autoconnect-priority", 0));
        }
        validator: IntValidator {
            bottom: -999
            top: 999
        }
        onEditingFinished: value => {
            const current = Number(root.editor.value("connection", "autoconnect-priority", 0));
            const n = value.trim() === "" ? 0 : parseInt(value, 10);
            if (!isNaN(n) && n !== current)
                root.editor.setValue("connection", "autoconnect-priority", n);
        }
    }

    SettingsDropdownRow {
        enabled: root.editable
        text: I18n.tr("Metered")
        options: root.meteredLabels
        currentValue: {
            root.rev;
            return root.meteredLabels[root.meteredIndex()];
        }
        onValueChanged: value => {
            const i = root.meteredLabels.indexOf(value);
            if (i >= 0 && i !== root.meteredIndex())
                root.editor.setValue("connection", "metered", i);
        }
    }

    SettingsDropdownRow {
        visible: root.zones.length > 0
        enabled: root.editable
        text: I18n.tr("Firewall zone")
        options: {
            root.rev;
            return root.zoneOptions();
        }
        currentValue: {
            root.rev;
            return root.storedZone() === "" ? I18n.tr("Default") : root.storedZone();
        }
        onValueChanged: value => {
            const zone = value === I18n.tr("Default") ? "" : value;
            if (zone !== root.storedZone())
                root.editor.setValue("connection", "zone", zone === "" ? undefined : zone);
        }
    }

    SettingsDropdownRow {
        enabled: root.editable
        text: I18n.tr("VPN")
        options: {
            root.rev;
            return root.vpnOptions();
        }
        currentValue: {
            root.rev;
            return root.vpnLabel();
        }
        onValueChanged: value => {
            const i = root.vpnOptions().indexOf(value);
            const uuid = i > 0 ? root.vpnProfiles[i - 1].uuid : "";
            if (uuid !== root.storedVpn())
                root.editor.setValue("connection", "secondaries", uuid === "" ? undefined : [uuid]);
        }
    }

    SettingsNoteRow {
        text: I18n.tr("All users of this computer can use this connection.")
        noteIconName: "info"
        tint: Theme.primary
        tintBackground: Theme.primaryHover
    }
}
